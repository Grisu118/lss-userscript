buildscript {
  ext.kotlin_version = '1.1.4'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id "com.eriwen.gradle.js" version "2.14.1"
}

apply plugin: 'kotlin2js'

repositories {
  mavenCentral()
  jcenter()
  maven { url 'https://jitpack.io' }
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
  compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.3"
}

build.doLast {
  configurations.compile.each { File file ->
    copy {
      includeEmptyDirs = false

      from zipTree(file.absolutePath)
      into "${projectDir}/web"
      include { fileTreeElement ->
        def path = fileTreeElement.path
        path.endsWith(".js") && (path.startsWith("META-INF/resources/") || !path.startsWith("META-INF/"))
      }
    }
  }

  (new File("${projectDir}/lssScript.js")).text = files("${projectDir}/web/kotlin.js", "${projectDir}/web/kotlinx-html-js.js", "${projectDir}/web/output.js").collect {
    it.getText()
  }.join("\n")
}

minifyJs {
  source = file("${projectDir}/lssScript.js")
  dest = file("${projectDir}/lssScript.min.js")
  closure {
    compilerOptions.languageIn = "ECMASCRIPT5"
  }
}

minifyJs.dependsOn build

compileKotlin2Js {
  kotlinOptions.outputFile = "${projectDir}/web/output.js"
  kotlinOptions.moduleKind = "plain"
  kotlinOptions.sourceMap = false
}

task("createRelease") {
 // Run for release
}

createRelease.dependsOn minifyJs

group 'ch.grisu118'
version '1.0-SNAPSHOT'